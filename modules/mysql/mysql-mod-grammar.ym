/*
 * Copyright (c) 2013 BalaBit IT Ltd, Budapest, Hungary
 * Copyright (c) 2013 Gyula Petrovics <pgyula@balabit.hu>
 *
 * This program is free software; you can redistribute it and/or modify it
 * under the terms of the GNU General Public License version 2 as published
 * by the Free Software Foundation, or (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program; if not, write to the Free Software
 * Foundation, Inc., 51 Franklin St, Fifth Floor, Boston, MA  02110-1301  USA
 *
 * As an additional exemption you are allowed to compile & link against the
 * OpenSSL libraries as published by the OpenSSL project. See the file
 * COPYING for details.
 *
 */

%code requires {

#include "mysql-mod-parser.h"

}

%code {

#include "cfg-grammar.h"
#include "cfg-parser.h"
#include "plugin.h"

}

%name-prefix "mysql_"

%lex-param {CfgLexer *lexer}
%parse-param {CfgLexer *lexer}
%parse-param {LogDriver **instance}
%parse-param {gpointer arg}


/* INCLUDE_DECLS */

%token KW_MYSQL
%token KW_DEFAULT
%token KW_RETRIES
%token KW_BULK_INSERT

%type <ptr> mysql_dd_values
%type <ptr> mysql_dd_values_build

%%

start
	: LL_CONTEXT_DESTINATION KW_MYSQL
	  {
	    last_driver = *instance = mysql_dd_new();
	  }
	  '(' mysql_dd_options ')'			{ YYACCEPT; }
	;

mysql_dd_options
	: mysql_dd_option mysql_dd_options
	|
	;

mysql_dd_option
	: KW_HOST '(' string ')'		{ mysql_dd_set_host(last_driver, $3); free($3); }
	| KW_PORT '(' LL_NUMBER ')'		{ mysql_dd_set_port(last_driver, $3); }
	| KW_USERNAME '(' string ')'		{ mysql_dd_set_user(last_driver, $3); free($3); }
	| KW_PASSWORD '(' string ')'		{ mysql_dd_set_password(last_driver, $3); free($3); }
	| KW_DATABASE '(' string ')'		{ mysql_dd_set_database(last_driver, $3); free($3); }
	| KW_TABLE '(' string ')'		{ mysql_dd_set_table(last_driver, $3); free($3); }
	| KW_BULK_INSERT '(' yesno ')'		{ mysql_dd_set_bulk_insert(last_driver, $3); }
	| KW_COLUMNS '(' string_list ')'	{ mysql_dd_set_columns(last_driver, $3); }
	| KW_INDEXES '(' string_list ')'	{ mysql_dd_set_indexes(last_driver, $3); }
	| KW_VALUES '(' mysql_dd_values ')'	{ mysql_dd_set_values(last_driver, $3); }
	| KW_NULL '(' string ')'		{ mysql_dd_set_null_value(last_driver, $3); free($3); }
	| KW_RETRIES '(' LL_NUMBER ')'		{ mysql_dd_set_retries(last_driver, $3); }
	| KW_FLUSH_LINES '(' LL_NUMBER ')'	{ mysql_dd_set_flush_lines(last_driver, $3); }
	| KW_FLUSH_TIMEOUT '(' LL_NUMBER ')'	{ mysql_dd_set_flush_timeout(last_driver, $3); }
	| KW_SESSION_STATEMENTS
	  '(' string_list ')'			{ mysql_dd_set_session_statements(last_driver, $3); }
	| dest_driver_option
	//| { last_template_options = mysql_dd_get_template_options(last_driver); } template_option
	;

mysql_dd_values
	: mysql_dd_values_build			{ $$ = g_list_reverse($1); }
	;

mysql_dd_values_build
	: string mysql_dd_values_build		{ $$ = g_list_append($2, g_strdup($1)); free($1); }
	| KW_DEFAULT mysql_dd_values_build	{ $$ = g_list_append($2, GUINT_TO_POINTER(MYSQL_COLUMN_DEFAULT)); }
	|					{ $$ = NULL; }
	;

/* INCLUDE_RULES */

%%
